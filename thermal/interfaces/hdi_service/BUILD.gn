# Copyright (c) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/ohos.gni")
import("../../thermal.gni")

ohos_shared_library("libthermal_interface_service_1.1") {
  include_dirs = [ "include" ]
  sources = [
    "src/base_info_config.cpp",
    "src/isolate_info_config.cpp",
    "src/sensor_info_config.cpp",
    "src/thermal_device_mitigation.cpp",
    "src/thermal_dfx.cpp",
    "src/thermal_hdf_config.cpp",
    "src/thermal_hdf_timer.cpp",
    "src/thermal_hdf_utils.cpp",
    "src/thermal_interface_impl.cpp",
    "src/thermal_simulation_node.cpp",
    "src/thermal_zone_manager.cpp",
  ]

  if (is_standard_system) {
    external_deps = [
      "drivers_interface_thermal:thermal_idl_headers",
      "hdf_core:libhdf_utils",
      "hilog:libhilog",
      "init:libbeget_proxy",
      "init:libbegetutil",
      "ipc:ipc_single",
      "libxml2:libxml2",
      "zlib:libz",
    ]
    if (has_thermal_c_utils_part) {
      external_deps += [ "c_utils:utils" ]
    }
    if (has_thermal_config_policy_part) {
      defines = [ "HAS_THERMAL_CONFIG_POLICY_PART" ]
      external_deps += [ "config_policy:configpolicy_util" ]
    }
  } else {
    external_deps = [
      "hilog:libhilog",
      "init:libbegetutil",
      "ipc:ipc_single",
      "libxml2:libxml2",
      "zlib:libz",
    ]
  }

  install_images = [ chipset_base_dir ]
  subsystem_name = "hdf"
  part_name = "drivers_peripheral_thermal"
}

##############################################################################################

ohos_shared_library("libthermal_driver") {
  include_dirs = [ "include" ]
  sources = [ "src/thermal_interface_driver.cpp" ]

  if (is_standard_system) {
    external_deps = [
      "drivers_interface_thermal:libthermal_stub_1.1",
      "hdf_core:libhdf_host",
      "hdf_core:libhdf_ipc_adapter",
      "hdf_core:libhdf_utils",
      "hdf_core:libhdi",
      "hilog:libhilog",
      "ipc:ipc_single",
    ]
    if (has_thermal_c_utils_part) {
      external_deps += [ "c_utils:utils" ]
    }
  } else {
    external_deps = [
      "hilog:libhilog",
      "ipc:ipc_single",
    ]
  }

  shlib_type = "hdi"
  install_images = [ chipset_base_dir ]
  subsystem_name = "hdf"
  part_name = "drivers_peripheral_thermal"
}

group("hdf_thermal") {
  deps = [
    ":libthermal_driver",
    ":libthermal_interface_service_1.1",
    "profile:thermal_hdf_config",
  ]
}
