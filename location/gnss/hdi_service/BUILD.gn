# Copyright (c) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
import("//build/ohos.gni")
import("../../location.gni")

config("libagnss_interface_service_cfg") {
  visibility = [":*"]
  include_dirs = [ "${location_driver_path}/gnss/hdi_service" ]
}

ohos_shared_library("libgnss_interface_service_2.0") {
  public_configs = [":libagnss_interface_service_cfg"]
  sources = [
    "gnss_interface_impl.cpp",
    "location_vendor_interface.cpp",
    "string_utils.cpp",
  ]

  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  branch_protector_ret = "pac_ret"

  cflags = [
    "-Wall",
    "-Wextra",
    "-Werror",
    "-fsigned-char",
    "-fno-common",
    "-fno-strict-aliasing",
  ]

  if (is_standard_system) {
    external_deps = [
      "c_utils:utils",
      "drivers_interface_location_gnss:location_gnss_idl_headers",
      "hdf_core:libhdi",
      "hilog:libhilog",
      "ipc:ipc_single",
    ]
  } else {
    external_deps = [
      "hilog:libhilog",
      "ipc:ipc_single",
    ]
  }

  defines = []
  if (is_emulator) {
    defines += [ "EMULATOR_ENABLED" ]
  }

  install_images = [ chipset_base_dir ]
  subsystem_name = "hdf"
  part_name = "drivers_peripheral_location_gnss"
}

ohos_shared_library("liblocation_gnss_hdi_driver") {
  include_dirs = [ "${location_driver_path}/gnss/hdi_service" ]

  sources = [ "gnss_interface_driver.cpp" ]

  cflags = [
    "-Wall",
    "-Wextra",
    "-Werror",
    "-fsigned-char",
    "-fno-common",
    "-fno-strict-aliasing",
  ]

  if (is_standard_system) {
    external_deps = [
      "c_utils:utils",
      "hdf_core:libhdf_host",
      "hdf_core:libhdf_ipc_adapter",
      "hdf_core:libhdf_utils",
      "hdf_core:libhdi",
      "hilog:libhilog",
      "ipc:ipc_single",
    ]
  } else {
    external_deps = [
      "hilog:libhilog",
      "ipc:ipc_single",
    ]
  }

  external_deps +=
      [ "drivers_interface_location_gnss:liblocation_gnss_stub_2.0" ]

  shlib_type = "hdi"
  install_images = [ chipset_base_dir ]
  subsystem_name = "hdf"
  part_name = "drivers_peripheral_location_gnss"
}

group("hdi_location_gnss_service") {
  deps = [
    ":libgnss_interface_service_2.0",
    ":liblocation_gnss_hdi_driver",
  ]
}
