# Copyright (c) 2021-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../sensor.gni")

config("libvdi_config") {
  include_dirs = [ "../interfaces/v1_0" ]
}

ohos_shared_library("libsensor_interface_service_2.0") {
  public_configs = [ ":libvdi_config" ]

  defines = []
  if (build_variant == "root") {
    defines += [ "SENSOR_DEBUG" ]
  }
  include_dirs = [
    ".",
    "../interfaces/include",
    "../interfaces/v1_0",
    "../utils/include",
  ]

  sources = [
    "sensor_callback_vdi.cpp",
    "sensor_client_info.cpp",
    "sensor_clients_manager.cpp",
    "sensor_hdi_dump.cpp",
    "sensor_if_service.cpp",
  ]

  cflags = [
    "-Wall",
    "-Wextra",
    "-Werror",
    "-fsigned-char",
    "-fno-common",
    "-fno-strict-aliasing",
  ]

  if (is_standard_system) {
    external_deps = [
      "drivers_interface_sensor:libsensor_stub_2.0",
      "hdf_core:libhdf_host",
      "hdf_core:libhdf_ipc_adapter",
      "hdf_core:libhdf_utils",
      "hilog:libhilog",
      "hitrace:hitrace_meter",
      "ipc:ipc_single",
    ]
    if (c_utils_enable) {
      external_deps += [ "c_utils:utils" ]
    }
  } else {
    external_deps = [
      "hilog:libhilog",
      "ipc:ipc_single",
    ]
  }

  install_images = [ chipset_base_dir ]
  subsystem_name = "hdf"
  part_name = "drivers_peripheral_sensor"
}

ohos_shared_library("libsensor_driver") {
  include_dirs = [
    "../interfaces/include/",
    "../utils/include",
  ]
  sources = [ "sensor_if_driver.cpp" ]

  cflags = [
    "-Wall",
    "-Wextra",
    "-Werror",
    "-fsigned-char",
    "-fno-common",
    "-fno-strict-aliasing",
  ]

  if (is_standard_system) {
    external_deps = [
      "drivers_interface_sensor:libsensor_stub_2.0",
      "hdf_core:libhdf_host",
      "hdf_core:libhdf_ipc_adapter",
      "hdf_core:libhdf_utils",
      "hdf_core:libhdi",
      "hilog:libhilog",
      "ipc:ipc_single",
    ]
    if (c_utils_enable) {
      external_deps += [ "c_utils:utils" ]
    }
  } else {
    external_deps = [
      "hilog:libhilog",
      "ipc:ipc_single",
    ]
  }

  shlib_type = "hdi"
  install_images = [ chipset_base_dir ]
  subsystem_name = "hdf"
  part_name = "drivers_peripheral_sensor"
}

group("hdi_sensor_service") {
  deps = [
    ":libsensor_driver",
    ":libsensor_interface_service_2.0",
  ]
}
