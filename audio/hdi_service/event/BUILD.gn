# Copyright (c) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

hdf_audio_path = "./../.."
hdf_hdi_service_path = "./.."
import("//build/ohos.gni")
import("$hdf_audio_path/audio.gni")

if (drivers_peripheral_audio_feature_hdf_proxy_stub == true) {
  ohos_shared_library("hdi_audio_pnp_server") {
    sources = [
      "$hdf_audio_path/hal/hdi_binder/server/src/hdf_audio_input_event.c",
      "$hdf_audio_path/hal/hdi_binder/server/src/hdf_audio_pnp_server.c",
      "$hdf_audio_path/hal/hdi_binder/server/src/hdf_audio_pnp_uevent.c",
      "$hdf_audio_path/hal/hdi_binder/server/src/hdf_audio_pnp_uevent_hdmi.c",
    ]

    include_dirs = [
      "$hdf_audio_path/hal/hdi_binder/server/include",
      "$hdf_hdi_service_path/primary_impl/include",
      "$hdf_hdi_service_path/vendor_interface/utils",
    ]

    defines = []
    if (enable_audio_hal_hdf_log) {
      defines += [ "AUDIO_HDF_LOG" ]
    }
    if (drivers_peripheral_audio_feature_double_pnp_detect) {
      defines += [ "AUDIO_DOUBLE_PNP_DETECT" ]
    }
    if (drivers_peripheral_audio_feature_community) {
      defines += [ "AUDIO_FEATURE_COMMUNITY" ]
    }

    if (is_standard_system) {
      external_deps = [
        "drivers_interface_audio:libaudio_proxy_5.0",
        "hdf_core:libhdf_host",
        "hdf_core:libhdf_ipc_adapter",
        "hdf_core:libhdf_utils",
        "hdf_core:libhdi",
        "hilog:libhilog",
      ]
      if (enable_c_utils) {
        external_deps += [ "c_utils:utils" ]
      }
    } else if (defined(ohos_lite)) {
      external_deps = [ "hilog_lite:hilog_shared" ]
    } else {
      external_deps = [ "hilog:libhilog" ]
    }
    external_deps += [ "bounds_checking_function:libsec_shared" ]

    shlib_type = "hdi"
    install_images = [ chipset_base_dir ]
    subsystem_name = "hdf"
    part_name = "drivers_peripheral_audio"
  }
}
