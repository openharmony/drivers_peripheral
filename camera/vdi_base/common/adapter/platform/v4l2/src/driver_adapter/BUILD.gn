# Copyright (c) 2021 - 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../../../../../../camera.gni")

config("v4l2_config") {
  visibility = [ ":*" ]

  cflags = [
    "-DGST_DISABLE_DEPRECATED",
    "-DHAVE_CONFIG_H",
    "-DCOLORSPACE=\"videoconvert\"",
  ]
  if (drivers_peripheral_camera_feature_board_config) {
    cflags += [
      "-Wall",
      "-Wextra",
      "-Werror",
      "-Wno-error",
      "-fno-strict-aliasing",
      "-Wno-sign-compare",
      "-Wno-builtin-requires-header",
      "-Wno-unused-variable",
      "-Wno-unused-label",
      "-Wno-implicit-function-declaration",
      "-Wno-format",
      "-Wno-int-conversion",
      "-Wno-unused-function",
      "-Wno-thread-safety-attributes",
      "-Wno-inconsistent-missing-override",
      "-fno-exceptions",
      "-ffunction-sections",
      "-fdata-sections",
    ]
  }

  if (enable_camera_device_utest) {
    cflags += [
      "-fprofile-arcs",
      "-ftest-coverage",
    ]

    ldflags = [ "--coverage" ]
  }
  if (drivers_peripheral_camera_feature_board_config) {
    include_dirs = [ "//device/board/${product_company}/${product_name}/camera/vdi_impl/v4l2/driver_adapter/include" ]
  } else {
    include_dirs = [ "./include" ]
  }
}

ohos_shared_library("peripheral_camera_v4l2_adapter") {
  defines = []
  if (drivers_peripheral_camera_feature_usb) {
    defines += [ "CAMERA_BUILT_ON_USB" ]
  }
  if (drivers_peripheral_camera_feature_board_config) {
    sources = [
      "//device/board/${product_company}/${product_name}/camera/vdi_impl/v4l2/driver_adapter/src/v4l2_buffer.cpp",
      "//device/board/${product_company}/${product_name}/camera/vdi_impl/v4l2/driver_adapter/src/v4l2_control.cpp",
      "//device/board/${product_company}/${product_name}/camera/vdi_impl/v4l2/driver_adapter/src/v4l2_dev.cpp",
      "//device/board/${product_company}/${product_name}/camera/vdi_impl/v4l2/driver_adapter/src/v4l2_fileformat.cpp",
      "//device/board/${product_company}/${product_name}/camera/vdi_impl/v4l2/driver_adapter/src/v4l2_stream.cpp",
      "//device/board/${product_company}/${product_name}/camera/vdi_impl/v4l2/driver_adapter/src/v4l2_uvc.cpp",
    ]
  } else {
    sources = [
      "$camera_path/dump/src/camera_dump.cpp",
      "src/v4l2_buffer.cpp",
      "src/v4l2_control.cpp",
      "src/v4l2_dev.cpp",
      "src/v4l2_fileformat.cpp",
      "src/v4l2_stream.cpp",
      "src/v4l2_uvc.cpp",
    ]
  }

  include_dirs = [
    "$camera_path/include",
    "$camera_path/dump/include",
    "$camera_path/../interfaces",
    "$camera_path/utils/event",
  ]
  deps = [ "$camera_path/utils:peripheral_camera_utils" ]
  if (is_standard_system) {
    external_deps = [
      "c_utils:utils",
      "graphic_surface:surface",
      "hdf_core:libhdf_host",
      "hdf_core:libhdf_utils",
      "hilog:libhilog",
    ]
  } else {
    external_deps = [ "hilog:libhilog" ]
  }
  external_deps += [
    "drivers_interface_camera:libbuffer_producer_sequenceable_1.0",
    "drivers_interface_camera:metadata",
  ]
  public_configs = [ ":v4l2_config" ]
  install_images = [ chipset_base_dir ]
  subsystem_name = "hdf"
  part_name = "drivers_peripheral_camera"
}
