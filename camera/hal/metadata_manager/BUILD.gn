# Copyright (c) 2022 - 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("../camera.gni")

if (!defined(ohos_lite)) {
  import("//build/ohos.gni")
  import("//drivers/hdf_core/adapter/uhdf2/uhdf.gni")

  config("camera_metadata_manager_config") {
    visibility = [ ":*" ]
    cflags = [
      "-DGST_DISABLE_DEPRECATED",
      "-DHAVE_CONFIG_H",
    ]

    if (enable_camera_device_utest) {
      cflags += [
        "-fprofile-arcs",
        "-ftest-coverage",
      ]

      ldflags += [ "--coverage" ]
    }
  }

  ohos_shared_library("camera_metadata_manager") {
    sources = [
      "$camera_path/metadata_manager/src/metadata_config.cpp",
      "$camera_path/metadata_manager/src/metadata_controller.cpp",
    ]
    include_dirs = [
      "$camera_path/../interfaces/include",
      "$camera_path/include",
      "$camera_path/device_manager/include",
      "$camera_path/metadata_manager/include",
    ]

    deps = [ "$camera_path/device_manager:camera_device_manager" ]

    if (is_standard_system) {
      external_deps = [
        "graphic_chipsetsdk:surface",
        "hdf_core:libhdf_host",
        "hdf_core:libhdf_ipc_adapter",
        "hdf_core:libhdf_utils",
        "hdf_core:libhdi",
        "hiviewdfx_hilog_native:libhilog",
        "ipc:ipc_single",
      ]
      if (use_hitrace) {
        external_deps += [ "hitrace_native:libhitrace" ]
      }
    } else {
      external_deps = [ "hilog:libhilog" ]
    }
    external_deps += [
      "drivers_interface_camera:metadata",
      "ipc:ipc_single",
    ]

    public_configs = [ ":camera_metadata_manager_config" ]
    install_images = [ chipset_base_dir ]
    subsystem_name = "hdf"
    part_name = "drivers_peripheral_camera"
  }
}
