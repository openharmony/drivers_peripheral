# Copyright (c) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/hdi/hdi.gni")
import("//build/ohos.gni")
import("./../../../usb.gni")

ohos_shared_library("libusbfn_mtp_interface_service_1.0") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  branch_protector_ret = "pac_ret"

  sources = [ "src/usbfn_mtp_impl.cpp" ]

  include_dirs = [
    "./../../../interfaces/ddk/device",
    "./../../../interfaces/ddk/common",
    "${usb_driver_path}/gadget/function/include",
    "${usb_driver_path}/gadget/function/mtp/include",
    "${usb_driver_path}/utils/include",
  ]

  deps = [ "${usb_driver_path}/ddk:libusb_ddk_device" ]

  if (is_standard_system) {
    external_deps = [
      "drivers_interface_usb:libusbfn_mtp_proxy_1.0",
      "hdf_core:libhdf_host",
      "hdf_core:libhdf_utils",
      "hilog:libhilog",
      "ipc:ipc_single",
    ]
    if (usb_c_utils_enable) {
      external_deps += [ "c_utils:utils" ]
    }
  } else {
    external_deps = [ "hilog:libhilog" ]
  }
  install_images = [ chipset_base_dir ]
  subsystem_name = "hdf"
  part_name = "drivers_peripheral_usb"
}

ohos_shared_library("libusbfn_mtp_driver") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
  }

  sources = [ "src/usbfn_mtp_interface_driver.cpp" ]

  include_dirs = [
    "./../../../interfaces/ddk/device",
    "./../../../interfaces/ddk/common",
    "${usb_driver_path}/gadget/function/include",
    "${usb_driver_path}/gadget/function/mtp/include",
    "${usb_driver_path}/utils/include",
  ]

  deps = []

  if (is_standard_system) {
    external_deps = [
      "drivers_interface_usb:libusbfn_mtp_stub_1.0",
      "hdf_core:libhdf_host",
      "hdf_core:libhdf_ipc_adapter",
      "hdf_core:libhdf_utils",
      "hdf_core:libhdi",
      "hilog:libhilog",
      "ipc:ipc_single",
    ]
    if (usb_c_utils_enable) {
      external_deps += [ "c_utils:utils" ]
    }
  } else {
    external_deps = [ "hilog:libhilog" ]
  }

  shlib_type = "hdi"
  install_images = [ chipset_base_dir ]
  subsystem_name = "hdf"
  part_name = "drivers_peripheral_usb"
}

group("hdi_usbfn_mtp_service") {
  deps = [
    ":libusbfn_mtp_driver",
    ":libusbfn_mtp_interface_service_1.0",
  ]
}
